plugins {
    id 'java-library'
}

group 'com.combatreforged.factory'
version '0.1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net'
    }
}

configurations {
    include {
        extendsFrom api
    }
    full {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api
    }
}

dependencies {
    api 'org.apache.logging.log4j:log4j-api:2.14.1'
    api 'org.apache.logging.log4j:log4j-core:2.14.1'

    api 'net.kyori:adventure-api:4.8.1'
    api 'net.kyori:adventure-text-serializer-gson:4.8.1'

    api 'com.mojang:brigadier:1.0.18'

    api 'org.jetbrains:annotations:22.0.0'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

task fullJar(type: Jar) {
    group = "build"
    archiveClassifier = "full"

    from (configurations.include.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/**"
    }

    with jar
}

artifacts {
    full fullJar
}

test {
    useJUnitPlatform()
}