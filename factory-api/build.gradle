plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.combatreforged.factory'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net'
    }
}

configurations {
    apiParts
    mcProvided
    api.extendsFrom apiParts, mcProvided
    include.extendsFrom api

    full {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api
    }
    fullNoDuplicates {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom apiParts
    }
}

dependencies {
    // make sure to use the correct versions of mcProvided artifacts for the Minecraft version (look into the version manifest)
    mcProvided 'org.apache.logging.log4j:log4j-api:2.8.1'
    mcProvided 'org.apache.logging.log4j:log4j-core:2.8.1'

    apiParts 'net.kyori:adventure-api:4.9.3'
    apiParts 'net.kyori:adventure-text-serializer-gson:4.9.3'

    mcProvided 'com.mojang:brigadier:1.0.17'

    apiParts 'org.jetbrains:annotations:22.0.0'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

task fullJar(type: Jar) {
    group = "build"
    archiveClassifier = "full"

    from (configurations.include.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/**"
    }

    with jar
}

task fullNoDuplicatesJar(type: Jar) {
    archiveClassifier = "full-no-duplicates"

    from (configurations.apiParts.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/**"
    }

    with jar
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifacts {
    archives fullJar
    full fullJar
    fullNoDuplicates fullNoDuplicatesJar
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = this.archivesBaseName

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Factory API'
                description = 'Factory API, a Minecraft-abstracted API for Fabric.'
                url = 'https://github.com/CombatReforged/factory'
                developers {
                    developer {
                        id = 'rizecookey'
                    }
                }
            }

            artifact fullJar
        }
    }
}