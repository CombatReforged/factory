plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.combatreforged.factory'
version '0.1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net'
    }
}

configurations {
    apiParts
    mcProvided
    api.extendsFrom apiParts, mcProvided
    include.extendsFrom api

    full {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api
    }
    fullNoDuplicates {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom apiParts
    }
}

dependencies {
    mcProvided 'org.apache.logging.log4j:log4j-api:2.14.1'
    mcProvided 'org.apache.logging.log4j:log4j-core:2.14.1'

    apiParts 'net.kyori:adventure-api:4.8.1'
    apiParts 'net.kyori:adventure-text-serializer-gson:4.8.1'

    mcProvided 'com.mojang:brigadier:1.0.18'

    apiParts 'org.jetbrains:annotations:22.0.0'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

task fullJar(type: Jar) {
    group = "build"
    archiveClassifier = "full"

    from (configurations.include.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/**"
    }

    with jar
}

task fullNoDuplicatesJar(type: Jar) {
    archiveClassifier = "full-no-duplicates"

    from (configurations.apiParts.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/**"
    }

    with jar
}

artifacts {
    archives fullJar
    full fullJar
    fullNoDuplicates fullNoDuplicatesJar
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = this.group
            artifactId = this.archivesBaseName
            version = this.version

            artifact jar
            artifact fullJar
        }
    }
}