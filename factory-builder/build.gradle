plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
	id 'java'
	id 'java-library'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "factory-builder"
version = "0.1.1-1.16_combat-6"
group = parent.group

repositories {
}

configurations {
	factory {
		canBeConsumed = false
		canBeResolved = true
	}
	api.extendsFrom factory
	include.extendsFrom factory

	includeClasspath
	implementation.extendsFrom includeClasspath
}

dependencies {
	minecraft "com.mojang:minecraft:1.16_combat-6"
	mappings minecraft.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:0.11.6"

	api project(":factory-api")
	include project(path: ":factory-api", configuration: "fullNoDuplicates")

	implementation project(":factory-test")

	/*includeClasspath "net.minecrell:terminalconsoleappender:1.2.0"
	includeClasspath "org.jline:jline-terminal-jna:3.20.0"
	includeClasspath "net.java.dev.jna:jna:5.9.0"*/ //TODO
}

minecraft {
	customManifest = "https://gist.githubusercontent.com/rizecookey/4c6142baaccc3875f9b227fe22f2ace5/raw/c8ed74b19f7a5315813c9d4b199798b692a8f359/1.16_combat-6.json"
	intermediaryUrl = (mcVer) -> "https://maven.combatreforged.com/net/fabricmc/intermediary/" + mcVer + "/intermediary-" + mcVer + "-v2.jar"
	accessWidener = file("src/main/resources/factorybuilder.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
	from(configurations.includeClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude "META-INF/**"
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = this.group
			artifactId = this.archivesBaseName
			version = this.version

			artifact(remapJar) {
				builtBy remapJar
			}
		}
	}
}
